#!/usr/bin/env coffee
# vim:ft=coffee
# -*- mode:coffee -*-

convertChapter = (str) ->
    str.replace /^\[chapter:(.+)\]$/igm, (f, h) ->
        "［＃２字下げ］#{h}［＃「#{h}」は大見出し］"

convertRuby = (str) ->
    str.replace /\[\[rb:([^ \[\]]+) > ([^ \[\]]+)\]\]/ig, (f, b, t) ->
        "｜#{b}《#{t}》"

convertNewpage = (str) ->
    (str.split "[newpage]").join "［＃改ページ］"

convertFigure = (str) ->
    str.replace /\[image:(.+\.(png|jpe?g))\]/ig, (f, n) ->
        "［＃挿絵（#{n}、横1200×縦1600）入る］"

SPECIAL_CHARS = [
    ["！？", "!?［＃「!?」は縦中横］"]
    ["！！", "!!［＃「!!」は縦中横］"]
    ["？！", "?!［＃「?!」は縦中横］"]
]

convertMarks = (str) ->
    SPECIAL_CHARS.reduce (prev, current) ->
        prev.replace (new RegExp current[0], "ig"), current[1]
    , str

main = () ->
    input = (require "fs").readFileSync "/dev/stdin", "utf-8"
    process.stdout.write convertRuby convertChapter convertNewpage convertFigure convertMarks input

main()

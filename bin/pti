#!/usr/bin/env coffee
# vim:ft=coffee
# -*- mode:coffee -*-

# pixiv novel syntax to InDesign tagged text
# http://help.adobe.com/ja_JP/indesign/cs/taggedtext/indesign_cs5_taggedtext.pdf


convertChapter = (str) ->
    str.replace /^\[chapter:(.+)\]$/igm, (f, h) ->
        "<ParaStyle:chapter>#{h}\n<ParaStyle:main>"

convertRuby = (str) ->
    str.replace /\[\[rb:([^ \[\]]+) > ([^ \[\]]+)\]\]/ig, (f, b, t) ->
        isGroupRuby = if b.length > 1 then "0" else "1"
        "<cRuby:1><cMojiRuby:#{isGroupRuby}><cRubyString:#{t}>#{b}<cRuby:><cMojiRuby:><cRubyString:>"

SPECIAL_CHARS = [
    ["--", "‑‑"]    #分離禁止ハイフン
    ["——", "<CharStyle:二倍ダーシ>―<CharStyle:>"]
    ["！？", "<cTatecuYoko:1>!?<cTatecuYoko:>"]
    ["！！", "<cTatecuYoko:1>!!<cTatecuYoko:>"]
    ["？！", "<cTatecuYoko:1>?!<cTatecuYoko:>"]
]

convertSpecialCharacters = (str) ->
    SPECIAL_CHARS.reduce (prev, current) ->
        prev.replace (new RegExp current[0], "ig"), current[1]
    , str

convertNewpage = (str) ->
    (str.split "[newpage]").join ""

trimEveryLine = (str) ->
    ((str.split "\n").map (l) ->
        l.trim()
    ).join "\n"

main = () ->
    input = (require "fs").readFileSync "/dev/stdin", "utf-8"
    process.stdout.write "<UNICODE-MAC>\n", "utf16le"
    process.stdout.write (convertSpecialCharacters convertRuby convertChapter convertNewpage trimEveryLine input), "utf16le"

main()
